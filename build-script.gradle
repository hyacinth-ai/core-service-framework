// plugins {
// id 'org.springframework.boot' version '2.1.3.RELEASE'
// }

// repositories {
//     maven { url 'https://repo.spring.io/libs-milestone' }
// }

//task allDependencies {
//    dependsOn allprojects.collect { "$it.path:dependencies" }
//}

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        // maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE'
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.0.0"
        classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8"
        classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.2.4" // id "org.flywaydb.flyway" version "5.2.4"
        // classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

task("dotDependencies") {
    doLast {
        def file = new File("project-dependencies.dot")
        file.delete()
        file << "digraph {\n"
        file << "splines=ortho\n"
        file << "ratio=0.6\n"
        
        rootProject.childProjects.each { item ->
            def from = item.value
            file << "\"${from.name}\"\n"
            from.configurations.compile.dependencies
                    .matching { it in ProjectDependency }
                    .each { to -> file << ("\"${from.name}\" -> \"${to.name}\"\n") }
        }
        file << "}\n"
    }
}


task("buildAll") {
    dependsOn(
        subprojects
            .collect({ sp -> ":${sp.name}" })
            .collect({ it + ':' + ((it.endsWith('server') || it.endsWith('service')) ? 'bootJar' : 'jar')})
    )
}

subprojects {

    version = '1.0.0.RELEASE' // default version

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'groovy'

    // apply plugin: 'checkstyle'
    // apply plugin: org.sonarqube.gradle.SonarQubePlugin

    apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin
    apply plugin: io.spring.gradle.dependencymanagement.DependencyManagementPlugin
    apply plugin: com.gorylenko.GitPropertiesPlugin
    apply plugin: com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormatPlugin
    apply plugin: org.flywaydb.gradle.FlywayPlugin

    ext {
        set('springCloudVersion', 'Greenwich.SR1')
    }

//    dependencies {
//        implementation 'org.springframework.boot:spring-boot-starter-actuator'
//        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//        implementation 'org.springframework.boot:spring-boot-starter-web'
//        implementation 'org.springframework.cloud:spring-cloud-config-server'
//        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
//        implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
//        testImplementation 'org.springframework.boot:spring-boot-starter-test'
//    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency 'io.swagger:swagger-annotations:1.5.22'
            dependency 'org.projectlombok:lombok:1.18.6'
            dependency 'io.springfox:springfox-swagger2:2.9.2'
            dependency 'io.springfox:springfox-swagger-ui:2.9.2'
            dependency 'com.google.code.findbugs:jsr305:3.0.0'
            dependency 'com.vladmihalcea:hibernate-types-52:2.4.3'
            dependency 'io.jsonwebtoken:jjwt-api:0.10.6'
            dependency 'io.jsonwebtoken:jjwt-impl:0.10.6'
            dependency 'io.jsonwebtoken:jjwt-jackson:0.10.6'
        }
    }

    repositories {
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        options.fork = true
        options.compilerArgs += ['-parameters', '-Xlint:unchecked']
    }

    flyway {
        cleanDisabled = true
    }
    
    jar { enabled = true }

    bootJar {
        classifier = 'boot'
        enabled = false
    }

    wrapper {
        description 'creates gradle wrapper in order to execute ./gradlew <task>'
        gradleVersion = '5.4'
        distributionUrl= 'https://services.gradle.org/distributions/gradle-5.4-all.zip'
    }

    bootRun { systemProperties System.properties }

    generateGitProperties { enabled = false }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://my.maven.test.com/repository/releases/") {
                    authentication(userName: "user", password: "password")
                }
            }
        }
    }

//    publishing {
//        publications {
//            bootJava(MavenPublication) {
//                artifact bootJar
//            }
//        }
//        repositories {
//            maven {
//                url 'https://repo.example.com'
//            }
//        }
//    }

    if (project.name.endsWith('service') || project.name.endsWith('server')) {
        defaultTasks 'clean', 'bootJar'

        bootJar {
            classifier = 'boot'
            enabled = true
        }

        generateGitProperties { enabled = new File('.git').exists() }

        springBoot {
            buildInfo {
                properties { additional = System.properties }
            }
        }
    }

    if (project.name.endsWith('client') || project.name.endsWith('api') || project.name.endsWith('support') || project.name.endsWith('lib')) {
        defaultTasks 'clean', 'jar'
    }

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    dependencies {
        annotationProcessor "org.projectlombok:lombok"

        compile 'org.projectlombok:lombok'
        compile 'org.slf4j:log4j-over-slf4j'
        compile 'javax.xml.bind:jaxb-api'
        compile 'org.glassfish.jaxb:jaxb-runtime'
        compile 'com.google.code.findbugs:jsr305'
    }
}
