// Gradle does not support `plugins` section when it is loaded as a external script like this.
// Instead, we have to declare plugin classpath (the legacy way) and apply the plugin main class manually.
// plugins {
// id 'org.springframework.boot' version '2.1.3.RELEASE'
// id "com.jfrog.bintray" version "1.+"
// id "org.flywaydb.flyway" version "5.2.4"
// }
// maven { url 'https://repo.spring.io/milestone' }

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.5.RELEASE"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.2"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath "gradle.plugin.org.flywaydb:gradle-plugin-publishing:6.3.3"
//        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
//        classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8"
    }
}

ext {
    set('springBootVersion', '2.2.5.RELEASE')
    set('springCloudVersion', 'Hoxton.SR3')
}

task("dotDependencies") {
    doLast {
        def file = new File("${project.projectDir}/project-dependencies.dot")
        file.delete()
        file << "digraph {\n"
        file << "splines=ortho\n"
        file << "ratio=0.6\n"

        rootProject.childProjects.each { item ->
            def from = item.value
            file << "\"${from.name}\"\n"
            from.configurations.compile.dependencies
                    .matching { it in ProjectDependency }
                    .each { to -> file << ("\"${from.name}\" -> \"${to.name}\"\n") }
        }
        file << "}\n"
    }
}

//task printAllDependencies {
//    dependsOn allprojects.collect { "$it.path:dependencies" }
//}

//task("debugAll") {
//    println(
//            subprojects
//                    .collect({ it.name })
//                    .findAll({ it.startsWith('core-service') })
//                    .collect({ it + ':bintrayUpload' })
//    )
//}

//task("publishAll") {
//    dependsOn(
//        subprojects
//            .collect({ it.name })
//            .findAll({ it.startsWith('core-service') })
//            .collect({ it + ':publish' })
//    )
//}

task("buildAll") {
    dependsOn(
        subprojects
            .collect({ sp -> ":${sp.name}" })
            .collect({ it + ':' + ((it.endsWith('server') || it.endsWith('service')) ? 'bootJar' : 'jar')})
    )
}

task("uploadAllToBintray") {
    dependsOn(
        subprojects
            .collect({ it.name })
            .findAll({ it.startsWith('core-service') })
            .collect({ it + ':bintrayUpload' })
    )
}

task("bumpVersion") {
    doFirst {
        def v = version.split('\\.')
        if (v.length == 3) {
            def ov = version
            v[2] = ((v[2] as int) + 1) as String
            def nv = v.join('.')
            file('version.txt').setText(nv)
            print("Bump from ${ov} to ${nv}")
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    // apply plugin: 'maven'
    // apply plugin: 'groovy'
    // apply plugin: org.sonarqube.gradle.SonarQubePlugin
    // apply plugin: com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormatPlugin

    apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin
    apply plugin: com.gorylenko.GitPropertiesPlugin
    apply plugin: org.flywaydb.gradle.FlywayPlugin
    apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

//    apply plugin: io.spring.gradle.dependencymanagement.DependencyManagementPlugin

//    dependencyManagement {
//        imports {
//            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
//        }
//        dependencies {
//            dependency 'org.projectlombok:lombok:1.18.10'
//            dependency 'io.swagger:swagger-annotations:1.5.22'
//            dependency 'io.springfox:springfox-swagger2:2.9.2'
//            dependency 'io.springfox:springfox-swagger-ui:2.9.2'
//            dependency 'com.google.code.findbugs:jsr305:3.0.2'
//            dependency 'com.vladmihalcea:hibernate-types-52:2.8.0'
//            dependency 'io.jsonwebtoken:jjwt-api:0.10.7'
//            dependency 'io.jsonwebtoken:jjwt-impl:0.10.7'
//            dependency 'io.jsonwebtoken:jjwt-jackson:0.10.7'
//            dependency 'de.codecentric:spring-boot-admin-starter-server:2.2.0'
//            dependency 'net.logstash.logback:logstash-logback-encoder:5.3'
//            // dependency 'javax.servlet:javax.servlet-api:4.0.1'
//        }
//    }

//    dependencies {
//        implementation 'org.springframework.boot:spring-boot-starter-actuator'
//        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//        implementation 'org.springframework.boot:spring-boot-starter-web'
//        implementation 'org.springframework.cloud:spring-cloud-config-server'
//        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
//        implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
//        testImplementation 'org.springframework.boot:spring-boot-starter-test'
//    }

    repositories {
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        options.fork = true
        options.compilerArgs += ['-parameters', '-Xlint:unchecked']
    }

    jar { enabled = true }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava // allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

   flyway {
       cleanDisabled = true
   }

    bootJar {
        classifier = 'boot'
        enabled = false
    }

//    wrapper {
//        description 'creates gradle wrapper in order to execute ./gradlew <task>'
//        gradleVersion = '6.1'
//        distributionUrl= 'https://services.gradle.org/distributions/gradle-6.1-all.zip'
//    }

    bootRun { systemProperties System.properties }

    generateGitProperties { enabled = false }

    if (project.findProperty('PUBLISHING_ENABLED') != null) {

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact tasks.sourcesJar
                    artifact tasks.javadocJar
                    if (project.name.endsWith('server')) {
                        // artifact tasks.bootJar
                    }
                    // groupId project.group
                    // artifactId project.name
                    // version project.version
                    pom {
                        name = "${project.group}:${project.name}"
                        description = project.description ?: "${project.group}:${project.name}"
                        url = project.PUBLISHING_SCM
                        licenses {
                            license {
                                name = project.PUBLISHING_LICENSE
                                url = project.PUBLISHING_LICENSE_URL
                            }
                        }
                        developers {
                            developer {
                                id = project.PUBLISHING_DEVELOPER_ID
                                name = project.PUBLISHING_DEVELOPER_NAME
                                email = project.PUBLISHING_DEVELOPER_EMAIL
                            }
                        }
                        scm {
                            connection = project.PUBLISHING_SCM
                            developerConnection = project.PUBLISHING_SCM
                            url = project.PUBLISHING_SCM
                        }
                    }
                }
            }

            repositories {
                if (project.PUBLISHING_TARGET == "local") {
                    maven {
                        name = 'local'
                        url = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
                    }
                }
                if (project.PUBLISHING_TARGET == "sonatype") {
                    maven {
                        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2" // "$buildDir/repos/releases"
                        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots" // "$buildDir/repos/snapshots"
                        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username = project.OSSRH_USERNAME
                            password = project.OSSRH_PASSWORD
                        }
                    }
                }
            }
        }

        if (project.PUBLISHING_TARGET.equals("sonatype")) {
            signing {
                sign publishing.publications.mavenJava
            }
        }

        if (project.PUBLISHING_TARGET.equals("bintray")) {
            bintray {
                user = project.BINTRAY_USER
                key = project.BINTRAY_API_KEY
                publish = Boolean.parseBoolean(project.BINTRAY_PUBLISH)
                pkg {
                    repo = project.BINTRAY_REPO
                    name = project.BINTRAY_PKG_NAME ?: "${project.group}:${project.name}"
                    licenses = [project.PUBLISHING_LICENSE]
                    vcsUrl = project.PUBLISHING_SCM
                    userOrg =  project.BINTRAY_ORG
                }
                publications = ['mavenJava']
            }
        }
    }

    if (project.name.endsWith('service') || project.name.endsWith('server')) {
        defaultTasks 'bootJar'

        bootJar {
            classifier = 'boot'
            enabled = true
        }

        springBoot {
            buildInfo {
                properties {
                    // additional = System.properties
                }
            }
        }

        generateGitProperties { enabled = new File('.git').exists() }

        dependencies {
            implementation 'org.springframework.boot:spring-boot-devtools'
        }
    } else {
        defaultTasks 'jar'
    }

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    dependencies {
        annotationProcessor "org.projectlombok:lombok:1.18.10"

        constraints {
            implementation 'org.projectlombok:lombok:1.18.10'
            implementation 'io.swagger:swagger-annotations:1.5.22'
            implementation 'io.springfox:springfox-swagger2:2.9.2'
            implementation 'io.springfox:springfox-swagger-ui:2.9.2'
            implementation 'com.google.code.findbugs:jsr305:3.0.2'
            implementation 'com.vladmihalcea:hibernate-types-52:2.8.0'
            implementation 'io.jsonwebtoken:jjwt-api:0.10.7'
            implementation 'io.jsonwebtoken:jjwt-impl:0.10.7'
            implementation 'io.jsonwebtoken:jjwt-jackson:0.10.7'
            implementation 'de.codecentric:spring-boot-admin-starter-server:2.2.0'
            implementation 'net.logstash.logback:logstash-logback-encoder:5.3'
            implementation 'javax.servlet:javax.servlet-api:4.0.1'
        }

        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")

        implementation 'org.projectlombok:lombok'
        implementation 'org.slf4j:log4j-over-slf4j'
        implementation 'javax.xml.bind:jaxb-api'
        implementation 'org.glassfish.jaxb:jaxb-runtime'
    }

}
